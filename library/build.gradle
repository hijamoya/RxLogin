apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // rxjava2
    compile 'io.reactivex.rxjava2:rxjava:2.0.5'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // facebook sdk
    compile('com.facebook.android:facebook-android-sdk:4.19.0@aar') {
        transitive = true
    }

    // google gms
    compile 'com.google.android.gms:play-services-auth:10.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    exclude '**/Manifest.java'
    include '**/*.java'

    showViolations true

    classpath = files()
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'RxLogin'

    publishedGroupId = 'com.hijamoya.rxlogin'
    libraryName = 'RxLogin'
    artifact = 'library'

    libraryDescription = 'RxLogin is an Android library that simplifies the process of login different types of services into a single observable.'

    siteUrl = 'https://github.com/hijamoya/RxLogin'
    gitUrl = 'https://github.com/hijamoya/RxLogin.git'

    libraryVersion = '0.1.0'

    developerId = 'hijamoya'
    developerName = 'Jam Hsu'
    developerEmail = 'hijamoya2004@hotmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

